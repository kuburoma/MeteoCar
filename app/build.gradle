apply plugin: 'com.android.application'
apply plugin: 'jacoco'

// -- Run for creating test result
// gradlew clean createDevelDebugCoverageReport jacocoTestReport
//
// -- Run sonar task
//gradlew sonarRunner

jacoco {

}
task jacocoTestReport(type: JacocoReport, dependsOn: "testDevelDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled = true
        html.enabled = true
    }
    classDirectories = fileTree(
            dir: 'build/intermediates/classes/devel/debug/cz/meteocar',
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/BuildConfig.*',
                       '**/Manifest*.*'])
    sourceDirectories = files('src/main/java')
    executionData = files('build/jacoco/testDebugUnitTest.exec')
    doFirst {
        files('build/intermediates/classes/debug').getFiles().each { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    configurations.all {
        resolutionStrategy {
            force 'com.android.support:support-annotations:23.0.1'
        }
    }

    defaultConfig {
        applicationId "cz.meteocar.unit"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 7
        versionName "1.0.6"
        testApplicationId "cz.meteocar.unit.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    productFlavors {
        devel {
            buildConfigField 'String', 'DATABASE', '"TEST_AndroidCarTracker.db"'
        }

        prod {
            buildConfigField 'String', 'DATABASE', '"AndroidCarTracker.db"'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            debuggable true
            testCoverageEnabled true
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}
apply plugin: 'sonar-runner'
sonarRunner {
    sonarProperties {
        property "sonar.host.url", "http://localhost:9000" // Address of Sonar server
        property "sonar.java.source", "7"
        property "sonar.sources", "src/main/java" // Sources
        property "sonar.projectName", "Meteocar" // Name of your project
        property "sonar.projectVersion", "1.0.2" // Version of your project
        property "sonar.projectDescription", "Sonar" // Description of your project
        property "sonar.tests", "src/test/java"
        property "sonar.jacoco.reportPath", "build/jacoco/testDevelDebugUnitTest.exec"
        property "sonar.jacoco.itReportPath", "build/outputs/code-coverage/connected/flavors/DEVEL/coverage.ec"
        property "sonar.junit.reportsPath", "build/test-results/debug"
        property "sonar.binaries", "build/intermediates/classes/devel/debug"

    }
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile files('libs/mbassador-1.2.0.jar')
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:support-v4:22.2.0'
    compile 'org.apache.commons:commons-lang3:3.0'
    compile 'com.jjoe64:graphview:4.0.1'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.google.guava:guava:19.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.powermock:powermock-api-mockito:1.6.1'
    testCompile 'org.powermock:powermock-module-junit4-rule-agent:1.6.1'
    testCompile 'org.powermock:powermock-module-junit4-rule:1.6.1'
    testCompile 'org.powermock:powermock-module-junit4:1.6.1'
    testCompile 'org.json:json:20140107'

    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.mockito:mockito-core:1.+'
    androidTestCompile "com.crittercism.dexmaker:dexmaker:1.4"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-dx:1.4"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-mockito:1.4"
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    androidTestCompile 'com.android.support:support-annotations:22+'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.5.4'
    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.2.0'
}